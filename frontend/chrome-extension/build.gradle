apply plugin: 'distribution'

configurations {
  content
}

dependencies {
  content project(path: ':frontend:chrome-extension:background', configuration: 'content')
  content project(path: ':frontend:chrome-extension:content', configuration: 'content')
  content project(path: ':frontend:chrome-extension:options', configuration: 'content')
}

distributions {
  release {
    baseName = 'sdk-search-release'

    contents {
      from('manifest.json') {
        expand(
            name: 'Android SDK Search',
            version: buildConfig.version.fullName,
            keyword: 'ad'
        )
      }
      from 'src/common/dist'
      from configurations.content
    }
  }
  debug {
    baseName = 'sdk-search-debug'

    contents {
      from('manifest.json') {
        expand(
            name: 'Android SDK Search (Debug)',
            version: buildConfig.version.fullName,
            keyword: 'ad2'
        )
      }
      from 'src/common/dist'
      from configurations.content
    }
  }
}

releaseDistTar.enabled = false
debugDistTar.enabled = false
